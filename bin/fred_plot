#!/usr/bin/perl
use strict;
use warnings;
use Env;
use Getopt::Std;

my %options = ();
getopts("ek:v:", \%options);
my $key = "";
my $varlist = "";
my $errorbars = exists $options{e};
$key = $options{k} if exists $options{k};
die "usage: fred_plot -k key -v [S|E|I|R|s|C|c|M|A|a|r] [-e]\n" if not $key;
$varlist = $options{v} if exists $options{v};
die "usage: fred_plot -k key -v [S|E|I|R|s|C|c|M|A|a|r] [-e]\n" if not $varlist;
my @args = check_varlist($varlist);

my $FRED = $ENV{FRED_HOME};
die "Please set environmental variable FRED_HOME to location of FRED home directory\n" if not $FRED;
my $bindir = "$FRED/bin";
my $id = `$bindir/fred_id $key`;
chomp $id;
die "fred_plot: UNKNOWN key: $key\n" if $id eq "UNKNOWN";

my $status = `$bindir/fred_status -k $key`;
chomp $status;
$status =~ s/\s.*//;
die "fred_plot: bad status: $status\n" if $status !~ /FINISHED/;

my $reportsdir = "$FRED/RESULTS/RUN/$id/DATA/REPORTS";

my $gnuplot = $ENV{FRED_GNUPLOT};
if (not $gnuplot or (not -x $gnuplot)) {
  my $x = $args[0];
  system "cat $reportsdir/$x.dat";
  exit;
}

my $outdir = "$FRED/RESULTS/RUN/$id/DATA/OUT";

chdir $reportsdir or die "Can't find REPORTS directory: $reportsdir\n";

my $pltfile = "sim-$key-$$.plt";
my $pngfile = "report-$key-$$.png";
open FH, ">$pltfile";
print FH <<"EOF";
set title \"FRED: Allegheny County Model\"
set terminal png font \"Arial Bold\" 12
set output \"$pngfile\"
set grid
set yrange [0:*]
set xrange [0:*]
set xlabel \"Days\" 0,0.5
set ylabel \"Individuals\" 1.2,0
EOF

for my $x (@args) {
  if ($x eq "AR" or $x eq "CAR") {
    print FH "set key left Left reverse\n";
    print FH "set ylabel 'Per cent of Population' 1.2,0\n";
  }
  if ($x eq "RR") {
    print FH "set ylabel 'Reproductive Rate' 1.2,0\n";
  }
}

my $n = 0;
for my $x (@args) {
  my $title;
  $title = "Cases" if $x eq "C";
  $title = "Susceptible" if $x eq "S";
  $title = "Exposed" if $x eq "E";
  $title = "Infectious" if $x eq "I";
  $title = "Recovered" if $x eq "R";
  $title = "Symptomatic" if $x eq "I_s";
  $title = "Immune" if $x eq "M";
  $title = "Attack Rate" if $x eq "AR";
  $title = "Clinical Attack Rate" if $x eq "CAR";
  $title = "Clinical Cases" if $x eq "CI";
  $title = "Reproductive Rate" if $x eq "RR";
  print FH ($n == 0)? "plot " : ",\\\n";
  $n++;
  print FH "\"$x.dat\" using 1:2:3 notitle with errorbars lw 1 lt $n, \\\n" if $errorbars;
  print FH "\"$x.dat\" using  1:2 title \"$title\" with lines lw 3 lt $n";
}
print FH "\n";
close FH;
system ($gnuplot,$pltfile);
print "$reportsdir/$pngfile\n";
exit;


sub check_varlist {
  my $v = shift;
  my @args = ();
  my %mapping = (C=>'C', S=>'S', E=>'E', I=>'I', R=>'R', s=>'I_s', M=>'M', A=>'AR', a=>'CAR', c=>'CI', r=>'RR');
  my @vars = split "", $v;
  for my $x (@vars) {
    if (exists $mapping{$x}) {
      push @args, $mapping{$x};
    }
    else {
      print "bad arg: |$x|\n";
      die "usage: fred_plot -k key -v [S|E|I|R|s|C|c|M|A|a|r]\n";
    }
  }
  return @args;
}

