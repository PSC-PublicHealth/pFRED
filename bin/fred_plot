#!/usr/bin/perl
use strict;
use warnings;
use Env;
use Getopt::Long qw(:config no_ignore_case bundling);

my %titles = ( AR => "Attack Rate",
	       C => "Cases",
	       CAR => "Clinical Attack Rate",
	       CI => "Clinical Incidents",
	       Cls => "Classroom Transmissions",
	       E => "Exposed",
	       H => "Household Transmissions",
	       I => "Infectious",
	       I_s => "Symptomatics",
	       M => "Immune",
	       N => "Pop Size",
	       Off => "Office Transmissions",
	       R => "Recovered",
	       RR => "Reproductive Rate",
	       S => "Susceptible",
	       Sch => "School Transmissions",
	       SM => "Seasonality Mult",
	       Wrk => "Workplace Transmissions",
	       X => "Imported Transmissions",
	       small => "Presenteeism at Small Workplaces",
	       med => "Presenteeism at Medium Workplaces",
	       large => "Presenteeism at Large Workplaces",
	       xlarge => "Presenteeism at Xlarge Workplaces",
);

my $key = "";
my @varlist = ();
my $year = "0";
my $xmin = "0";
my $xmax = "*";
my $ymin = "0";
my $ymax = "*";
my $errorbars;
my $title = "FRED: Allegheny County Model";
my $fontsize = "giant";

my $opt_result = GetOptions("e" => \$errorbars,
			    "f=s" => \$fontsize,
			    "k=s" => \$key,
			    "v=s" => \@varlist,
			    "a=i" => \$year,
			    "x=i" => \$xmin,
			    "X=i" => \$xmax,
			    "y=i" => \$ymin,
			    "Y=i" => \$ymax,
			    "T=s" => \$title);

my $usage = "usage: fred_plot -k key -v [-a start_year|-e|-f fontsize|-x xmin|-y ymin|-X xmax|-Y ymax|-T title]\n";

die $usage if not $key;
die $usage if not @varlist;

my $FRED = $ENV{FRED_HOME};
die "Please set environmental variable FRED_HOME to location of FRED home directory\n" if not $FRED;
my $bindir = "$FRED/bin";
my $id = `$bindir/fred_id $key`;
chomp $id;
die "fred_plot: UNKNOWN key: $key\n" if $id eq "UNKNOWN";

my $status = `$bindir/fred_status -k $key`;
chomp $status;
$status =~ s/\s.*//;
if ($status !~ /FINISHED/) {
  print "fred_plot: bad status: $status\n";
  exit;
}

my $FREDRESULTS = $ENV{FRED_RESULTS};
$FREDRESULTS = $ENV{FRED_HOME} if not $FREDRESULTS;
my $reportsdir = "$FREDRESULTS/RESULTS/JOB/$id/DATA/REPORTS";

my $gnuplot = $ENV{FRED_GNUPLOT};
if (not $gnuplot or (not -x $gnuplot)) {
  my $x = $varlist[0];
  system "cat $reportsdir/$x.dat";
  exit;
}

my $outdir = "$FREDRESULTS/RESULTS/JOB/$id/DATA/OUT";

chdir $reportsdir or die "Can't find REPORTS directory: $reportsdir\n";

my $pltfile = "sim-$key-$$.plt";
my $pngfile = "report-$key-$$.png";
open FH, ">$pltfile";
print FH <<"EOF";
set title \"$title\"
# set terminal png font \"Arial Bold\" 12
set terminal png $fontsize 
set output \'$pngfile\'
set grid
set xrange [$xmin:$xmax]
set yrange [$ymin:$ymax]
set xlabel \'Days\' offset 0,0.5
set ylabel \'Individuals\' offset 1.2,0
set xtics out
EOF
if ($year) {
  print FH "set xlabel 'Year' 0,0.5\n";
  print FH "set xtics rotate by 90 1\n";
  print FH "set mxtics 4\n";
}

my $n = 0;
for my $x (@varlist) {
  my $title = $x;
  $title = $titles{$x} if exists $titles{$x};
  print FH ($n == 0)? "plot " : ",\\\n";
  $n++;
  if ($year) {
    print FH "\"$x.dat\" using ($year + (\$1/365)):2:3 notitle with errorbars lw 1 lt $n, \\\n" if $errorbars;
    print FH "\"$x.dat\" using  ($year + (\$1/365)):2 title \"$title\" with lines lw 3 lt $n";
  }
  else {
    print FH "\"$x.dat\" using 1:2:3 notitle with errorbars lw 1 lt $n, \\\n" if $errorbars;
    print FH "\"$x.dat\" using  1:2 title \"$title\" with lines lw 3 lt $n";
  }
}
print FH "\n";
close FH;
system ($gnuplot,$pltfile);
print "$reportsdir/$pngfile\n";
exit;



